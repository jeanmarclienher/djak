class Aparser {
field buffer _states;
field Alexer lex;
field Atoken lookahead;
field string lookstring;
field int status;
field int debug;
field Aclass _class;

const int Croot := 1;
const int Cclass_dec := 2;
const int Cstatement := 3;
const int Cstatements := 4;
const int Cexpression := 5;
const int Creturn_statem1 := 6;
const int Cexpression1 := 7;
const int Cterm := 8;
const int Cterm1 := 9;
const int Cterm2 := 10;
const int Cexpr_list := 11;
const int Cexpr_list1 := 12;
const int Csub_call1 := 13;
const int Clet_statem1 := 14;
const int Clet_statem2 := 15;
const int Cdo_statem1 := 16;
const int Cwhile_statem1 := 17;
const int Cwhile_statem2 := 18;
const int Csubrout_body1 := 19;
const int Csubroutine_dec := 20;
const int Cclass_dec1 := 21;
const int Cif_statem1 := 22;
const int Cif_statem2 := 23;
const int Cbool_expr := 24;
const int Cbool_expr1 := 25;
const int Cbool_term := 26;
const int Cbool_term1 := 27;
const int Ccomp_op := 28;
const int Celse_statem := 29;
const int Celse_statem1 := 30;


constructor Aparser new()
{
	debug := 0;
	status := 0;
	lex := null;
	_states := buffer.new(8);
	_class := null;
	push(Aparser.$Croot);
	return this;
}

method void dispose()
{
	_states.dispose();
	std.free(this);
	return;
}

method string get_data()
{
	return lookstring;
}

method Aclass get_next()
{
	return _class;
}

method int parse(in Alexer lexer_)
{
	lex := lexer_;
	while (lex.empty() == 0) {
		lookahead := lex.get_at(0);
		lookstring := lookahead.get_data();
		if ((lookahead.get_type() == Alexer.$Cnewline) 
		 	|| (lookahead.get_type() == Alexer.$Ccomment))
		{
			eat();
		} else {
			state_machine();
		}
		if (lookahead.get_type() == Alexer.$Cend) {
			return 0;
		}
	}
	return 0;
}

method void error(in string e)
{
	console.log_str("found '");
	console.log_str(lookahead.get_data());
	console.log_str("' at line(");
	console.log_int(lookahead.get_line());
	console.log_str("): ");
	console.log(e);
	std.exit(-1);
	status := -1;
	return;
}

method void push(in int step_)
{
	_states.push(step_);
	return;
}


method void pop()
{
	_states.pop();
	return;
}

method int root()
{
	if (status != 0) {
		return -1;
	}
	push(Aparser.$Cclass_dec);
	return 0;
}

method void class_dec()
{
	if (match_id("class") == 0) {
		error("expecting 'class' keyword");
		return;
	}
	eat();
	if (match_id(null) == 0) {
		error("expecting identifier");
		return;
	}

	_class := Aclass.new(lookahead);
	eat();

	if (match_op("{") == 0) {
		error("expecting '{'");
		return;
	}
	eat();
	while (status >= 0) {
		class_var_dec();
	}
	status := 0;
	while (status >= 0) {
		clas_const_dec();
	}
	status := 0;
	push(Aparser.$Cclass_dec1);
	push(Aparser.$Csubroutine_dec);
	return;
}

method void class_dec1()
{
	status := 0;
	if (match_op("}") == 0) {
		error("expecting '}'");
		return;
	}
	eat();
	pop();
	return;
}

method void class_var_dec()
{
	if (match_id("field") != 0) {
		_class.prepare_kind(Aclass.$Cfield);
	} else if (match_id("static") != 0) {
		_class.prepare_kind(Aclass.$Cstatic);
	} else {
		status := -1;
		return;
	}
	eat();

	type();

	var_name();	

	while (match_op(",") != 0) {
		_class.valid_member_name();
		var_name();
	}
	if (match_op(";") == 0) {
		error("';' expected.");
		return;
	}
	_class.valid_member_name();
	eat();
	return;
}

method void var_name()
{
	if (match_id(null) != 0) {
		_class.prepare_set_name(lookahead);
		eat();
		return;
	}
	error("Name of variable expected.");
	return;
}

method void type() 
{
	if (match_id("int") != 0) {
		_class.prepare_type(Aclass.$Cint);
	} else if (match_id("char") != 0) {
		_class.prepare_type(Aclass.$Cchar);
	} else if (match_id(null) != 0) {
		_class.prepare_type_identifier(lookahead);
	} else {
		error("Type expected.");
		return;
	}
	eat();
	return;
}

method void clas_const_dec()
{
	if (match_id("const") != 0) {
		_class.prepare_kind(Aclass.$Cconst);
	} else {
		status := -1;
		return;
	}
	eat();

	type();

	var_name();	

	if (match_op(":=") == 0) {
		error("':=' expected.");
		return;
	}
	eat();

	if (match_op(";") == 0) {
		_class.finish_const(lookahead);
	} else {
		error("Expected constant initialiser.");
		return;
	}
	eat();
	if (match_op(";") == 0) {
		error("';' expected.");
		return;
	}
	eat();
	return;
}

method void subroutine_dec()
{
	if (status != 0) {
		pop();
		return;
	}
	if (match_id("constructor") != 0) {
		_class.prepare_kind(Aclass.$Cconstructor);
	} else if (match_id("function") != 0) {
		_class.prepare_kind(Aclass.$Cfunction);
	} else if (match_id("method") != 0) {
		_class.prepare_kind(Aclass.$Cmethod);
	} else if (match_id("virtual") != 0) {
		_class.prepare_kind(Aclass.$Cvirtual);
	} else {
		pop();
		status := -1;
		return;
	}
	eat();
	if (match_id("void") != 0) {
		_class.prepare_type(Aclass.$Cvoid);
		eat();
	} else {
		type();
	}

	var_name(); // subrout_name	

	if (match_op("(") == 0) {
		error("'(' expected.");
		return;
	}
	eat();

	param_list();

	if (match_op(")") == 0) {
		error("')' expected.");
		return;
	}
	eat();

	subrout_body();


	return;
}

method void param_list()
{
	if (match_op(")") != 0) {
		return;
	}
	
	_class.start_param_list();	

	direction();
	
	type();

	var_name(); 
	
	while (match_op(",") != 0) {
		_class.finish_param();
		eat();
		direction();
		type();
		var_name();
	}	
	_class.finish_param();
	return;
}

method void direction()
{
	if (match_id("in") != 0) {
		eat();
		return;
	} else if (match_id("out") != 0) {
		eat();
		return;
	} else if (match_id("io") != 0) {
		eat();
		return;
	}
	error("Expected 'in/out/io/'.");
	return;
}
	
method void subrout_body()
{
	if (match_op("{") == 0) {
		error("'{' expected.");
		return;
	}
	eat();

	while (status == 0) {
		var_dec();
	}
	status := 0;

	push(Aparser.$Csubrout_body1);
	push(Aparser.$Cstatements);
	return;
}

method void subrout_body1()
{
	pop();
	if (match_op("}") == 0) {
		error("'}' expected at end of subroutine body.");
		return;
	}
	eat();
	_class.finish_subroutine();
	return;
}

method void var_dec()
{
	if (match_id("var") == 0) {
		status := 1;
		return;
	}
	eat();

	if (match_id(null) == 0) {
		error("class name or variable type expected.");
		return;
	}
	eat();
	if (match_id(null) == 0) {
		error("variable name expected.");
		return;
	}
	eat();
	if (match_op(";") == 0) {
		error("';' expected at end of variable declaration");
		return;
	}
	eat();
	return;
}

method void statements()
{
	if (status == 0) {
		push(Aparser.$Cstatement);
		return;
	}
	if (status > 0) {
		status := 0;
	}
	pop();
	return;
}

method void statement()
{
	var int r;
	pop();
	r := 0;
	if (match_id("break") != 0) {
		r := 1;
		eat();
		if (match_op(";") == 0) {
			error("';' expected after break");
			return;
		}
		eat();
		return;
	}
	if (r == 0) { r := if_statem(); }
	if (r == 0) { r := while_statem(); }
	if (r == 0) { r := return_statem(); }
	if (r == 0) { r := dolet_statem(); }
	if (r == 0) {
		// end of subroutine or end of statement body
		status := 1;
	}
	return;
}

method int if_statem()
{
	if (match_id("if") == 0) {
		return 0;
	}
	eat();
	if (match_op("(") == 0) {
		error("'(' expected after if.");
		return 1;
	}
	eat();
	push(Aparser.$Cif_statem1);
	push(Aparser.$Cbool_expr);

	return 1;
}

method void if_statem1()
{
	pop();
	if (match_op(")") == 0) {
		error("')' expected after if condition.");
		return;
	}
	eat();
	if (match_op("{") == 0) {
		error("'{' expected after if ().");
		return;
	}
	eat();
	push(Aparser.$Celse_statem);
	push(Aparser.$Cstatements);
	return;
}

method void else_statem()
{
	pop();
	if (match_op("}") == 0) {
		error("'}' expected after if body.");
		return;
	}
	eat();
	if (match_id("else") == 0) {
		return;
	}
	eat();
	
	if (if_statem() != 0) {
		return;
	}

	if (match_op("{") == 0) {
		error("'{' expected after else.");
		return;
	}
	eat();
	push(Aparser.$Celse_statem1);
	push(Aparser.$Cstatements);
	return;
}

method void else_statem1()
{
	pop();
	if (match_op("}") == 0) {
		error("'}' expected after else body.");
		return;
	}
	eat();
	return;
}



method int while_statem()
{
	if (match_id("while") == 0) {
		return 0;
	}
	eat();
	if (match_op("(") == 0) {
		error("'(' expected after while.");
		return 1;
	}
	eat();
	push(Aparser.$Cwhile_statem1);
	push(Aparser.$Cexpression);
	return 1;
}

method void while_statem1()
{
	pop();
	if (match_op(")") == 0) {
		error("')' expected after while condition.");
		return;
	}
	eat();
	if (match_op("{") == 0) {
		error("'{' expected after while ().");
		return;
	}
	eat();
	push(Aparser.$Cwhile_statem2);
	push(Aparser.$Cstatements);
	return;
}

method void while_statem2()
{
	pop();
	if (match_op("}") == 0) {
		error("'}' expected after while body.");
		return;
	}
	eat();
	return;
}

method int return_statem()
{
	if (match_id("return") == 0) {
		return 0;
	}
	eat();
	if (match_op(";") != 0) {
		eat();
	} else {
		push(Aparser.$Creturn_statem1);
		push(Aparser.$Cexpression);
	}
	return 1;
}


method void return_statem1()
{
	if (match_op(";") != 0) {
		eat();
		pop();
		return;
	}
	error("';' expected");
	return;
}

method int dolet_statem()
{
	var Atoken id;
	if (match_id(null) == 0) {
		return 0;
	}
	id := lookahead;
	eat();	
	if ((match_op(":=") != 0) || (match_op("[") != 0)) {
		return let_statem(id);
	} else if (match_op("}") == 0) {
		return do_statem(id);
	}
	return 0;
}


method int let_statem(in Atoken id)
{
	
	if (match_op(":=") != 0) {
		eat();
		push(Aparser.$Clet_statem1);
		push(Aparser.$Cexpression);
		return 1;
	} else if (match_op("[") != 0) {
		eat();
		push(Aparser.$Clet_statem2);
		push(Aparser.$Cexpression);
		return 1;
	}
	return 0;
}

method void let_statem1()
{
	pop();
	if (match_op(";") == 0) {
		error("';' expected at end of let statement");
		return;
	}
	eat();
	return;
}

method void let_statem2()
{
	pop();
	if (match_op("]") == 0) {
		error("']' expected in left side of let statement");
		return;
	}
	eat();
	if (match_op(":=") != 0) {
		eat();
		push(Aparser.$Clet_statem1);
		push(Aparser.$Cexpression);
		return;
	}
	error("':=' expected in let statement");
	return;
}
method int do_statem(in Atoken id)
{
	push(Aparser.$Cdo_statem1);
	return subrout_call(id);
}

method void do_statem1()
{
	pop();
	if (match_op(";") == 0) {
		error("';' expected at end of do statement");
		return;
	}
	eat();
	return;
}


method void bool_expr()
{
	pop();
	push(Aparser.$Cbool_expr1);
	push(Aparser.$Cbool_term);
	return;
}

method void bool_expr1()
{
	if (bool_op() != 0) {
		push(Aparser.$Cbool_term);
		return;
	} else {
		// bool expr end
		pop();
	}
	return;
}

method void expression()
{
	pop();
	push(Aparser.$Cexpression1);
	push(Aparser.$Cterm);
	return;
}

method void expression1()
{
	if (op() != 0) {
		push(Aparser.$Cterm);
		return;
	} else {
		// validate expression
		pop();
	}
	return;
}

method void comp_op()
{
	pop();
	if (match_op("==") != 0) {
	} else if (match_op("!=") != 0) {
	} else if (match_op(">=") != 0) {
	} else if (match_op("<=") != 0) {
	} else if (match_op(">") != 0) {
	} else if (match_op("<") != 0) {
	} else {
		error("comparaison opertaor expected.");
		return;
	}
	eat();
	return;
}

method int bool_op()
{
	if (match_op("&&") != 0) {
	} else if (match_op("||") != 0) {
	} else {
		return 0;
	}
	eat();
	return 1;
}

method int op()
{
	if (match_op("+") != 0) {
	} else if (match_op("-") != 0) {
	} else if (match_op("*") != 0) {
	} else if (match_op("/") != 0) {
	} else if (match_op("%") != 0) {
	} else {
		return 0;
	}
	eat();
	return 1;
}

method void bool_term()
{
	var int not;
	pop();
	not := 0;
	if (match_op("!")) {
		eat();
		not := 1;
	}
	if (match_op("(") != 0) {
		eat();
		push(Aparser.$Cbool_term1);
		push(Aparser.$Cbool_expr);
		return;
	} else if (not != 0) {
		error("'(' expected after '!'.");
		return;
	}
	push(Aparser.$Cexpression);
	push(Aparser.$Ccomp_op);
	push(Aparser.$Cexpression);
	return;
}

method void bool_term1()
{
	pop();
	if (match_op(")") != 0) {
		eat();
		return;
	}
	error("missing ')' in bool term.");
	return;
}

method void term()
{
	var Atoken id;
	pop();
	if (match_id("this") != 0) {
		// key_const
		eat();
	} else if (match_id("null") != 0) {
		// key_const
		eat();
	} else if (lookahead.get_type() == Alexer.$Cidentifier) {
		id := lookahead;
		eat();
		if (match_op("[") != 0) {
			// var_name "[" expression "]"
			eat();
			push(Aparser.$Cterm2);
			push(Aparser.$Cexpression);
		} else if (subrout_call(id) != 0) {
		} else if (match_op(".$") != 0) {
			// class_name ".$" var_name
			eat();
			if (lookahead.get_type() != Alexer.$Cidentifier) {
				error("constant name expected.");
				return;
			}
			eat();
		} else {
			// var_name
			id := id;
		}
	} else if (lookahead.get_type() == Alexer.$Cquote) {
		// char_const
		eat();
	} else if (lookahead.get_type() == Alexer.$Cstring) {
		// str_const
		eat();
	} else if (lookahead.get_type() == Alexer.$Cnumber) {
		// int_const
		eat();
	} else if (match_op("-") != 0) {
		// unary_op
		eat();
		push(Aparser.$Cterm);
	} else if (match_op("+") != 0) {
		// unary_op
		eat();
		push(Aparser.$Cterm);
	} else if (match_op("(") != 0) {
		// "(" expression ")"
		eat();
		push(Aparser.$Cterm1);
		push(Aparser.$Cexpression);
	} else {
		error("unexpected term");
		return;
	}
	return;
}

method int subrout_call(in Atoken id)
{
	if (match_op("(") != 0) {
		// subrout_call
		sub_call(id);
		return 1;
	} else if (match_op(".") != 0) {
		// subrout_call
		id := id;
		eat();
		id := lookahead;
		eat();
		sub_call(id);
		return 1;
	}
	return 0;
}
	
method void term1()
{
	// "(" expression ")"
	pop();
	if (match_op(")") != 0) {
		eat();
		return;
	}
	error("')' expected after term");
	return;
}

method void term2()
{
	// var_name "[" expression "]"
	pop();
	if (match_op("]") != 0) {
		eat();
		return;
	}
	error("']' expected after term");
	return;
}

method void sub_call(in Atoken id)
{
	// id "(" expr_:list ")"
	if (match_op("(") == 0) {
		error("'(' expected in subroutine call.");
		return;
	}
	eat();
	
	push(Aparser.$Csub_call1);
	if (match_op(")") == 0) {
		push(Aparser.$Cexpr_list);
		push(Aparser.$Cexpression);
		return;
	}
	return;
}

method void sub_call1()
{
	//  ")"
	pop();
	if (match_op(")") != 0) {
		eat();
		return;
	}
	error("')' expected at end of subroutrine call");
	return;
}

method void expr_list()
{
	pop();
	if (status != 0) {
		status := 0;
		return;
	}
	if (match_op(",") != 0) {
		eat();
		push(Aparser.$Cexpr_list1);
		push(Aparser.$Cexpression);
	}
	return;
}

method void expr_list1()
{
	pop();
	if (status != 0) {
		return;
	}
	if (match_op(",") != 0) {
		eat();
		push(Aparser.$Cexpr_list1);
		push(Aparser.$Cexpression);
	}
	return;
}

method void eat()
{
	lex.shift();
	lookahead := lex.get_at(0);
	while ((lookahead.get_type() == Alexer.$Cnewline) 
		 	|| (lookahead.get_type() == Alexer.$Ccomment))
	{
		lex.shift();
		lookahead := lex.get_at(0);
	}
	lookstring := lookahead.get_data();
	return;
}

method int match_id(in string id)
{
	if (lookahead.get_type() != Alexer.$Cidentifier) {
		return 0;
	}
	if (id == null) {
		return 1;
	}
	if (lookstring.compare(id) == 0) {
		return 1;
	}
	return 0;
}

method int match_op(in string id)
{
	if (lookahead.get_type() != Alexer.$Coperator) {
		return 0;
	}
	if (lookstring.compare(id) == 0) {
		return 1;
	}
	return 0;
}

method int match_newline()
{
	if (lookahead.get_type() == Alexer.$Cnewline) {
		return 1;
	}
	return 0;
}

method int match_str(in string id)
{
	var int t;
	
	t := lookahead.get_type();
	if ((t != Alexer.$Cstring) && (t != Alexer.$Cquote)) {
		return 0;
	}
	if ((id == null) || (lookstring.compare(id) == 0)) {
		return 1;
	}
	return 0;
}

method int state_machine()
{
	var int state;

	state := _states.get_last();

	if (debug > 0) {	
		console.log(lookstring);
		console.log_int(state);
	}
	if (state == Aparser.$Croot) {
		root();
	} else if (state == Aparser.$Cclass_dec) {
		class_dec();
	} else if (state == Aparser.$Cstatement) {
		statement();
	} else if (state == Aparser.$Cstatements) {
		statements();
	} else if (state == Aparser.$Cexpression) {
		expression();
	} else if (state == Aparser.$Cexpression1) {
		expression1();
	} else if (state == Aparser.$Cterm) {
		term();
	} else if (state == Aparser.$Cterm1) {
		term1();
	} else if (state == Aparser.$Cterm2) {
		term2();
	} else if (state == Aparser.$Creturn_statem1) {
		return_statem1();
	} else if (state == Aparser.$Cexpr_list) {
		expr_list();
	} else if (state == Aparser.$Cexpr_list1) {
		expr_list1();
	} else if (state == Aparser.$Csub_call1) {
		sub_call1();
	} else if (state == Aparser.$Clet_statem1) {
		let_statem1();
	} else if (state == Aparser.$Clet_statem2) {
		let_statem2();
	} else if (state == Aparser.$Cdo_statem1) {
		do_statem1();
	} else if (state == Aparser.$Cwhile_statem1) {
		while_statem1();
	} else if (state == Aparser.$Cwhile_statem2) {
		while_statem2();
	} else if (state == Aparser.$Csubrout_body1) {
		subrout_body1();
	} else if (state == Aparser.$Csubroutine_dec) {
		subroutine_dec();
	} else if (state == Aparser.$Cclass_dec1) {
		class_dec1();
	} else if (state == Aparser.$Cif_statem1) {
		if_statem1();
	} else if (state == Aparser.$Celse_statem) {
		else_statem();
	} else if (state == Aparser.$Celse_statem1) {
		else_statem1();
	} else if (state == Aparser.$Cbool_expr) {
		bool_expr();
	} else if (state == Aparser.$Cbool_expr1) {
		bool_expr1();
	} else if (state == Aparser.$Cbool_term) {
		bool_term();
	} else if (state == Aparser.$Cbool_term1) {
		bool_term1();
	} else if (state == Aparser.$Ccomp_op) {
		comp_op();
	} else {
		error("unexpected state");
		lex.shift();
		status := -1; 
	}
	return 1;
}


} // class

